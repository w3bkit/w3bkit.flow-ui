prototype(W3bkit.FlowUI:Document.HTML) < prototype(Neos.Fusion:Component) {

    doctype = ${ '<!DOCTYPE html>' }

    @context.viewport = W3bkit.FlowUI:Viewport {
        width = ${ FlowUIdocument.viewport.width || 'device-width' }
        scale = ${ FlowUIdocument.viewport.scale || 1 }
    }

    @context.bootstrap = W3bkit.FlowUI:Bootstrap {
        style = ${ FlowUIdocument.bootstrap.style }
        minify = ${ FlowUIdocument.bootstrap.minify }
        cache = ${ FlowUIdocument.bootstrap.cache }
        script = ${ FlowUIdocument.bootstrap.script }
        icons = ${ FlowUIdocument.bootstrap.icons }
    }

    @context.flowuijs = Neos.Fusion:Tag {
        tagName = 'script'
        attributes {
            src = Neos.Fusion:ResourceUri {
                path = 'resource://W3bkit.FlowUI/Public/JavaScript/flowui.js'
            }
        }
    }

    head = Neos.Fusion:Tag {
        tagName = ${ 'head' }
        content = Neos.Fusion:Component {
            renderer = afx`
                <meta charset='utf-8' />
                { viewport }
                <Neos.Fusion:Loop @if.render={ FlowUIdocument.meta } items={ FlowUIdocument.meta } >
                    <meta name={ itemKey } content={ item } />
                </Neos.Fusion:Loop>
                { bootstrap }
                <Neos.Fusion:Loop @if.render={ FlowUIdocument.cdn } items={ FlowUIdocument.cdn } >
                    <W3bkit.FlowUI:CDN data={ item } />
                </Neos.Fusion:Loop>
                { flowuijs }
            `
        }
    }

    renderer = afx`
        { props.doctype }
        <html lang={ FlowUIdocument.html.lang || 'en' } >
            { props.head }
            <body>
                { props.content }
            </body>
        </html>
    `

}