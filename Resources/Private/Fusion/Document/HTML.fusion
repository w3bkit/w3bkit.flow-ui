prototype(W3bkit.FlowUI:Document.HTML) < prototype(Neos.Fusion:Component) {

    doctype = ${ '<!DOCTYPE html>' }

    @context.viewport = W3bkit.FlowUI:Viewport {
        width = ${ FlowUIconfig.viewport.width || 'device-width' }
        scale = ${ FlowUIconfig.viewport.scale || 1 }
    }

    @context.bootstrap = W3bkit.FlowUI:Bootstrap {
        style = ${ FlowUIconfig.bootstrap.style }
        minify = ${ FlowUIconfig.bootstrap.minify }
        cache = ${ FlowUIconfig.bootstrap.cache }
        script = ${ FlowUIconfig.bootstrap.script }
        icons = ${ FlowUIconfig.bootstrap.icons }
    }

    @context.flowuijs = Neos.Fusion:Tag {
        tagName = 'script'
        attributes {
            src = Neos.Fusion:ResourceUri {
                path = 'resource://W3bkit.FlowUI/Public/JavaScript/flowui.js'
            }
        }
    }

    head = Neos.Fusion:Tag {
        tagName = ${ 'head' }
        content = Neos.Fusion:Component {
            renderer = afx`
                <meta charset='utf-8' />
                { viewport }
                <Neos.Fusion:Loop items={ FlowUIconfig.meta } >
                    <meta name={ itemKey } content={ item } />
                </Neos.Fusion:Loop>
                { bootstrap }
                <Neos.Fusion:Loop items={ FlowUIconfig.cdn } >
                    <W3bkit.FlowUI:CDN data={ item } />
                </Neos.Fusion:Loop>
                { flowuijs }
            `
        }
    }

    renderer = afx`
        {props.doctype}
        <html lang={ FlowUIconfig.html.lang || 'en' } >
            {props.head}
            <body>
                {props.content}
            </body>
        </html>
    `

}